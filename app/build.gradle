buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.parse.com/repo' }
    }
    dependencies {
        classpath 'com.parse.tools:gradle:1.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'me.tatarka:gradle-retrolambda:2.5.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.parse'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "net.rdrei.android.connectivityalert"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.buildConfigField "String", "PARSE_APPLICATION_ID", "\"" + parseApplicationId + "\""
        variant.buildConfigField "String", "PARSE_MASTER_KEY", "\"" + parseMasterKey + "\""
    }
    dexOptions {
        incremental false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

parse {
    applicationId parseApplicationId
    masterKey parseMasterKey
    // This makes it automatic. Otherwise you would have to run e.g. gradlew parseUploadSymbolsRelease.
    uploadSymbols true
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:1.8.1'
    compile 'com.parse.bolts:bolts-android:1.1.4'
    compile 'io.reactivex:rxjava:1.0.3'
    compile 'io.reactivex:rxandroid:0.23.0'
    compile 'com.google.dagger:dagger:2.0-SNAPSHOT'
    provided 'javax.annotation:jsr250-api:1.0'
    provided 'javax.inject:javax.inject:1'
    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.android.support:support-annotations:21.0.3'

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
}
